/JOB
//NAME HANDLING-TOOL-CALIBRATION
//POS
///NPOS 2,0,0,6,0,0
///TOOL 1
///POSTYPE PULSE
///PULSE
C00000=-756,-120016,-78578,-5263,-22038,102319
C00001=-555,-119067,-76539,0,-22986,99236
///TOOL 0
P00000=0,-222014,-26224,0,-132993,0
///TOOL 5
///POSTYPE TOOL
///RECTAN
///RCONF 0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0
P00111=13.256,-316.051,34.225,-89.9940,0.0000,0.2183
///TOOL 0
///POSTYPE BASE
P00112=13.256,-316.051,34.225,-90.0000,0.0000,0.0000
///TOOL 1
///RCONF 1,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0
P00120=1571.243,-1.499,-518.376,90.0000,0.0000,90.0000
P00123=1492.407,-1.092,-518.767,89.9951,0.0000,90.2253
P00124=1492.407,-1.092,-518.767,89.9951,0.0000,90.2253
//INST
///DATE 2018/08/27 13:30
///ATTR SC,RW
///GROUP1 RB1
///LVARS 20,2,10,0,0,30,10,0
NOP
IFTHEN IN#(741)=ON
	 GETREG LI000 MREG#(1)
	 SET LB000 LI000
	 JUMP LABEL: LB000
ENDIF
'********************************
'** Check and  TCP program **
'********************************
' 
' ** original tool data Tool 1 **
'X  6.653
'Y   -317.023
'Z   37.887
'RX  -90.0000
'RY  0.0
'RZ  0.0
'********************************
'------------------------SET TOOL
SET LB011 1
'----------------SET DIRECT INPUT
SET LB012 2
'----------------SET LIMITS IN mm
SET LD004 5
'--------------------------------
MOVJ P0000 VJ=50.00
'*****
SETREG MREG#(1) 1
MOVL C00000 V=833.3
'*****
SETREG MREG#(1) 2
MOVJ P0120 VJ=10.00  //Positie boven sensor
TIMER T=0.30
GETE LD000 P0123 (1)
ADD LD000 5000
SETE P0123 (1) LD000
MOVL P0123 V=83.3 PL=0  //berekende positie
'------------------------
'-----------PREPARING LP VARIABLE
MUL LD004 1000
SET LD000 1
*15
GETS LPX[LD000] $PX000
CNVRT LPX[LD000] LPX[LD000] RF
SUB LP[LD000] LP[LD000]
ADD LD000 1
JUMP *15 IF LD000<21
SETE LP001 (3) -10000
SETE LP002 (3) 1000
SETE LP003 (1) 15000
SETE LP004 (1) -15000
SETE LP005 (2) 15000
SETE LP006 (2) -15000
SETE LP007 (1) -5000
IMOV LP001 V=5.0 PL=0 TF SRCH RIN#(LB012)=ON T=0.30 DIS=50.0
IMOV LP002 V=33.3 PL=0 TF
TIMER T=0.20
IMOV LP003 V=5.0 PL=0 TF SRCH RIN#(LB012)=ON T=0.30 DIS=10.0
GETS LPX008 $PX001
IMOV LP007 V=33.3 PL=0 TF
TIMER T=0.20
IMOV LP004 V=5.0 PL=0 TF SRCH RIN#(LB012)=ON T=0.30 DIS=10.0
GETS LPX009 $PX001
ADD LP008 LP009
DIV LP008 2
MOVL LP008 V=66.7
TIMER T=0.20
IMOV LP005 V=5.0 PL=0 TF SRCH RIN#(LB012)=ON T=0.30 DIS=10.0
GETS LPX009 $PX001
MOVL LP008 V=66.7
TIMER T=0.20
IMOV LP006 V=5.0 PL=0 TF SRCH RIN#(LB012)=ON T=0.30 DIS=10.0
GETS LPX008 $PX001
CNVRT LPX008 LPX008 RF
CNVRT LPX009 LPX009 RF
ADD LP008 LP009
DIV LP008 2
MOVL LP008 V=33.3 PL=0
'-----------------------------
TIMER T=0.50
'-----------------------------
SUB P0111 P0111
GETS PX0122 $PX000
GETS PX0124 $PX000
TIMER T=0.20
'** Met instellen van programma
'Hier waardes van P124
'invullen in pulse data **
SUB P0124 P0124
SETE P0124 (1) -583
SETE P0124 (2) -129229
SETE P0124 (3) -99341
SETE P0124 (4) 0
SETE P0124 (5) -11706
SETE P0124 (6) 99256
TIMER T=0.20
*124
CNVRT PX0124 PX0124 UF#(21) TL#(2)
CNVRT PX0122 PX0122 UF#(21) TL#(6)
MSHIFT PX0111 TF PX0122 PX0124
'-----------------------------
GETTOOL P0112 TL#(2)
GETE LD007 P0112 (1)
GETE LD008 P0112 (2)
GETE LD009 P0112 (3)
GETE LD001 P0111 (1)
GETE LD002 P0111 (2)
GETE LD003 P0111 (3)
SUB LD001 LD007
SUB LD002 LD008
SUB LD003 LD009
SET I016 LD001
SET I017 LD002
SET I018 LD003
'********************************
GETTOOL LP000 TL#(1)
'********************************
'weg schrijven verschil op IF-PAN
DIV I016 100
DIV I017 100
DIV I018 100
'berekenen nieuwe tool data
MUL LD001 1
MUL LD002 1
MUL LD003 1
ADD LD007 LD001
ADD LD008 LD002
ADD LD009 LD003
SETE P0112 (1) LD007
SETE P0112 (2) LD008
SETE P0112 (3) LD009
SETE P0112 (4) -900000
SETE P0112 (5) 0
SETE P0112 (6) 0
'********************************
GETTOOL LP000 TL#(2)
SET LP001 P0112
'*****
SUB LP000 LP001
GETE LD000 LP000 (1)
GETE LD001 LP000 (2)
GETE LD002 LP000 (3)
SET LB000 0
MUL LD004 1000
SET LD005 LD004
MUL LD005 -1
IFTHEN LD000>LD004 ORIF LD000<LD005
	 SET LB000 1
ENDIF
IFTHEN LD001>LD004 ORIF LD001<LD005
	 SET LB000 1
ENDIF
IFTHEN LD002>LD004 ORIF LD002<LD005
	 SET LB000 1
ENDIF
'********************************
IFTHEN LB000=0
	'wegschrijven nieuwe tool data
	'nieuwe positie tcp in sensor
	 SETTOOL TL#(1) P0112
ELSE
	 SETUALM 8000 "STRING" 0
ENDIF
*100
GETS PX0124 $PX000
CNVRT PX0124 PX0124 BF TL#(1)
SET P0123 P0124
*END
MOVL P0120 V=83.3 PL=0
'%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
SFTOF
SET B050 1
*OVER
*2
MOVL C00001 V=400.0
'*****
SETREG MREG#(1) 2
*1
MOVL P0000 V=833.3
'*****
SETREG MREG#(1) 1
END
